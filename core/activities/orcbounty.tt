#nop core/activities/orcbounty.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: activities;

/* Orc Bounty Requests {{{ */
/* Prevents false triggerings from other people's requests */
#act {^You nod at the orc pursuer.} {
	#var _orcDidNod 1;
	#delay {nodExp} {
		#unvar _orcDidNod;
	} {1};
}

/* You were given if bounty, but only if you just nodded to him! */
#act {^'Ekai Nosrat Sulla Garha "%*", Setama Sulla "%*", Ekass Armas Thran Bel Dinas.' the orc pursuer says. 'Nohsa Sulla Amata.'} {
	#if {${_orcDidNod}}{
		#class tmp_orcCmd read core/activities/lib/orcbounty.tt;
		_gotBounty {%1} {%2};
		#class tmp_orcCmd kill;
		_event_raise {activity_bounty} {start} {%1} {%2};
	};
	#unvar _orcDidNod;
}
/* }}} */

/* Orc Bounty Rewards {{{ */
#act {^The orc pursuer gives you %*.} {
	#if {"${_orcLoaded}" != "1"} {
		#class tmp_orcCmd read core/activities/lib/orcbounty.tt;
	};
	_rewBounty {%1};
	_event_oneshot {prompt_post} {orcFin} {_orcFin};
} {3}
/* }}} */

/* Player (bounty) command {{{ */
#alias {bounty} {
	#class tmp_orcCmd read core/activities/lib/orcbounty.tt;
	_doBounty %1;
	#class tmp_orcCmd kill;
}
/* }}} */
