#nop lib/math.tt
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: math;
#nop PROVIDES: lib/math;

/*
	Usage: @math_eval{ <math expression> }
	Returns: the value of the expression after evaluation
*/
#function {math_eval} {
	#math result {%0};
}

/*
	Usage: @math_max{ {<value>} {<value>} }
	Returns: the value of the greater input value
*/
#function {math_max} {
	#if {%1 > %2} {
		#return %1;
	} {
		#return %2;
	};
}

/*
	Usage: @math_min{ {<value>} {<value>} }
	Returns: the value of the lesser input value
*/
#function {math_min} {
	#if {%1 > %2} {
		#return %2;
	} {
		#return %1;
	};
}

/*
	Usage: @math_abs{ <value> }
	Returns: the absolute value of the argument
*/
#function {math_abs} {
	#math result { @math_sign{%0} * %0};
}

/*
	Usage: @math_sign{ <value> }
	Returns: 1 if value is positive, -1 if negative, and 0 if zero
*/
#function {math_sign} {
	#if { %0 > 0} {
		#var result 1;
	};
	#elseif { %0 < 0 } {
		#var result {-1};
	};
	#else {
		#var result 0;
	};
}

/*
	Usage: @decrement{ <value> }
	Returns: the value--
*/
#function {decrement} {
	#math result {%1 - 1};
}

/*
	Usage: @dec2zero{ <value> }
	Returns: the value-- or 0, whichever is greater
*/
#function {dec2zero} {
	#var result @math_max{ {0} {@decrement{%1}} };
}

/* Verify loading */
#var loadState {1};
/* EOF */
