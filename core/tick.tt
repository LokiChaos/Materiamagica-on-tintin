#nop core/tick.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: tick;
#nop PROVIDES: core/tick;

/* Master Tick Timer {{{ */

/*
	To Sync the tick timer:
		_event_raise {tick_sync};
	By default, this is tied to some Auction Events and Hints
*/

/* Event to trigger tick syncing */
_event_register {tick_sync} {sync} {_SyncTick};

#alias {_SyncTick} {
	#nop %1;
	#read core/tick/rtlib/sync.tt;
};

/* Main 15 second timer to track ticks, incriment game timer, etc. 
	Calls other subroutines defined in other files, each should
	have an associated #nop describing where it is defined
*/
#tick {master} {
	_OnTick
} {15};

#alias {_OnTick} {
	_event_raise {tick};
	#read core/tick/rtlib/ontick.tt;
};
/* }}} */

/* sysinfo.txt updating {{{ */

/* Every 10 minutes re-sync sysinfo.txt w/ iota */
#tick {sysinfo} {
	_UpdateSysInfo
} {600};

/* Downloads sysinfo.txt and parses the information into tintin */
#alias {_UpdateSysInfo} {
	_Class load tmp/core/sysinfo read core/tick/rtlib/sysinfo.tt;
	_UpdateSystemInfo;
	_Class purge tmp/core/sysinfo;
}

/* Prevents spamming iota with requests */
#alias {_GetSysinfo} {

	_Class load tmp/core/sysinfo read core/tick/rtlib/sysinfo.tt;
	_GetSysteminfo;
	_Class purge tmp/core/sysinfo;
}
/* }}} */
