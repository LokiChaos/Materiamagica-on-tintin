#nop lib/time.tt
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: time;
#nop PROVIDES: lib/time;

/*
	Usage: @getTime{}
	Returns: current time in HH:MM:SS format
*/
#function {getTime} {
	#format {result} {%t} {%H:%M:%S};
}

/*
	Usage: @getDate{}
	Returns: current time in YYYY-MM-DD format
*/
#function {getDate} {
	#format {result} {%t} {%Y-%m-%d};
}

/*
	Usage: @getTimestamp{}
	Returns: current time in YYYY-MM-DD HH:MM:SS format
*/
#function {getTimestamp} {
	#format {result} {%t} {%Y-%m-%d %H:%M:%S};
}

/*
	Usage: @getEpoch{}
	Returns: current Unix Epoch time
*/
#function {getEpoch} {
	#format {result} {%T};
}

/*
	Usage: @getuEpoch{}
	Returns: current micro Unix Epoch time
*/
#function {getuEpoch} {
	#format {result} {%U};
}

/*
	Usage: @getTimeDelta{ <time> }
	<time> = Unix Epoch time
	Returns: time difference between current time and <time> in seconds
	         A negative value means <time> is in the future
*/
#function {getTimeDelta} {
	#math result {@getEpoch{} - %1};
}

/*
	Usage: @getuTimeDelta{ <utime> }
	<utime> = micro Unix Epoch time
	Returns: time difference between current time and <time> in microseconds
	         A negative value means <utime> is in the future
*/
#function {getuTimeDelta} {
	#math result {@getuEpoch{} - %1};
}

/*
	Usage: @unixToTime{ {<unix Epoch time>} [{<format>}]}
	Returns: given Unix epoch time in format or default %c
*/
#function {unixToTime} {
	#if {"%1" == ""} {
		Print {ERROR} {unixToTime: argument error};
	};
	#else {
		#if {"%2" != ""} {
			#format {result} {%t} {{%2} {%1}};
		} {
			#format {result} {%t} {{%c} {%1}};
		};
	};
}

/* Verify loading */
#var loadState {1};
/* EOF */
