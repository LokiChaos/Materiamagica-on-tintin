#nop core/prompt.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: prompt;
#nop PROVIDES: core/prompt;
#nop REQUIRES: core/state core/gmcp;

/*  Setup Status bar display and prompt triggers {{{
	
	You can customize the prompt/staus bar here, however this code is not
	pretty and modifying it will take a fair bit of work.  I have tried to
	modualrize and profusely comment this code to make it as comprehensable
	as I can.

	Note, that most of this gets executed for EVERY prompt.  For this reason
	I felt that speed/efficiency was more important than transparancy.

	As a general rule, try to put subunits into prompt/subStrings.tt
	Also try to unvar any non-persistent variables as soon as possible.

This is generally what it would look like with most everything maxed/true:	
[==============================================================================]
[SAFE][N E S W U D Nw Ne Sw Se]  [LAGED][CASTING][100%][1234x,1234y][g F][18:06]
[99% LongCharName][100%][M:9999st][                   ][249 250 245 132 106 114]
[ScSsBlP*ArShSaStFsLsAw AaHsEhDg][PaBlSiSlWbPoPlCuFfWk][LvPdUwLwSeDnDiDm HiSnSh]
<2750hp 3500sp 2750st> [+2700 -1250 =____] [H][T][Enc][PANIC][Rx#][BoT][Mail][I]

Other tags: [Search][Dig][Q][B][C][T][SW][S][Q][A][News][TAG!]
(This looks a lot better in CoLoRs!)
 }}} */

/* Dummy values to prevent errors before values are properly set {{{ */
#if {&vitals[] == 0} {
	#var vitals[max][hp] 5000;
	#var vitals[max][sp] 5000;
	#var vitals[max][st] 5000;
	#var gtime[regen]   100;
	#var gtime[time]      0;
	#var _exitsC        {N E S W U D Nw Ne Sw Se};
}
/* }}} */

/* Reserve area for the status bar, and set the prompt's format */
#split  {$prefs[split][top]} {$prefs[split][bot]};

/* Add/Remove Prompt Flags {{{ */
#alias {_promptFlagAdd} {
	#var prompt[modFlags][%1] {{v}{%2} {flag}{%3} {a}{%3}};
}

#alias {_promptFlagDel} {
	#unvar prompt[modFlags][%1];
}
/* }}} */

/* Extracts mounts stamina and gags the line {{{ */
#act {^(Mount: %dst)} {
	#var mount[stamina]  {%1};
	#var stateChar[mounted] 2;
	#line gag;
}
/* }}} */

/* Only Update Affects Bar On Affects Changes {{{ */
_Event_register {affect_change}  {updatePrompt} {_UpdatePromptAffects};
_Event_register {affect_refresh} {updatePrompt} {_UpdatePromptAffects};

#alias {_UpdatePromptAffects} {
	#nop %1;
	_Class !load tmp/core/affBar core/prompt/rtlib/affectbar.tt;
	#var prompt[2] {@PromptAffectBar{}};
	_Class purge tmp/core/affBar;
}
/* }}} */

/* Bind to EOR / Prompt Event and Raise prompt_* Events {{{ */
#event {RECEIVED PROMPT}
{
	#var prompt[raw] {%1};
	#var __promptType {Unknown};

	#if {"%1" == "%*<%dhp %dsp %dst> "}
		{#var __promptType {Default};
	};
	#elseif {"%1" == "[C]ontinue [R]edraw [B]ack [E]xit: "}
		{#var __promptType {Continue};
	};
	#elseif {"%1" == "%*({Social|#.*|@#.*}) "}
		{#var __promptType {Social};
	};
	#elseif {"%1" == "%*(%Shull %Sdir %S %Swind%*"}
		{#var __promptType {Ship};
	};
	#elseif {"%1" == "] "}
		{#var __promptType {Write};
	};
	
	_Event_raise {prompt_pre} {${__promptType}} {%1};
	_prompt${__promptType} {%1};
	_Event_raise {prompt_${__promptType}} {%1};
	#if {"${__promptType}" != "Continue" } {
		_Event_raise {prompt_post} {${__promptType}} {%1};
	};
	#unvar __promptType;
}

/* Gag extra prompt strings */
#gag {%*<%dhp %dsp %dst> $};
/* }}} */

/* Default Prompt {{{ */
#alias {_promptDefault} {
	_Class !load tmp/core/prompt/lines core/prompt/rtlib/subprompt.tt;
	#regex {%1} {<%dhp %dsp %dst>} {
		#var vitals[o] {$vitals[c]};
		#var vitals[c][hp] {&1};
		#var vitals[c][sp] {&2};
		#var vitals[c][st] {&3};
	};

	#math stateChar[mounted] @dec2zero{$stateChar[mounted]};
	#math stateChar[Combat]  @dec2zero{$stateChar[Combat]};

	#var stateChar[inSocial] 0;
	#var stateChar[OnShip]   0;

	_calcVitals;
	#var vitals[print] @promptVitalsBar{};

	#var prompt[1] {@left_right{ {$vitals[print]} {@promptFlagsBar{}} }};

	_PromptExtrasBar;

	#var prompt[-1] {@PromptTop{}};

	_PromptNavBar;

	_PromptEcho;

	_PromptTick;

	#line gag;
	#var stateChar[justLooking] 0;
	#math client[State][muteLine] @dec2zero{$client[State][muteLine]};
	_Class purge tmp/core/prompt/lines;
}
/* }}} */

/* Ship Prompt {{{ */
#alias {_promptShip}
{
	#regex {%1} {%*(%Shull %Sdir %S %Swind} {
		#format prompt[1] 
		{<149><<099>%s<149>hull <099>%s<149>dir<099> %s %s<149>wind}
		{&2} {&3} {&4} {&5} {&6};
	};
	#regex {%1} {%d\%shld} {
		#format prompt[1] {%s <099>%s%a<149>shld>} {$prompt[1]} {&1} {37};
	} {
		#var prompt[1] {${prompt[1]}><099>};
	};
	#var stateChar[OnShip] 1;
	_Class !load tmp/core/prompt/lines core/prompt/rtlib/subprompt.tt;
	#var prompt[1] {@left_right{ {$prompt[1]} {@promptFlagsBar{}} }};
	#var prompt[-1] {@PromptTop{}};

	_PromptEcho;

	_PromptNavBar;
	_PromptTick;
	#line gag;
	#var stateChar[justLooking] 0;
	#math client[State][muteLine] @dec2zero{$client[State][muteLine]};
	_Class purge tmp/core/prompt/lines;
}
/* }}} */

/* Social Prompt {{{ */ 
#alias {_promptSocial}
{
	#var stateChar[inSocial] 1;
	#regex {%1} {%*({@|}{Social|#.*}) } {
		#format {prompt[1]} {<099>[<139>&2<148>&3<099>]<098>};
		#if { "&3" != "Social" } {
			#var stateChar[inChatRoom] {&3};
			#replace stateChar[inChatRoom] {#} {};
		} {
			#unvar stateChar[inChatRoom];
		};
	};
	_Class !load tmp/core/prompt/lines core/prompt/rtlib/subprompt.tt;

	#format prompt[1] {%-40s} {$prompt[1]};
	#var prompt[1] {@left_right{ {$prompt[1]} {@promptFlagsBar{}} }};
	#var prompt[-1] {@PromptTop{}};
	#var prompt[3] @blankPrompt{};
	_PromptEcho;
	_PromptNavBar;
	_PromptTick;
	#line gag;
	#var stateChar[justLooking] 0;
	#math client[State][muteLine] @dec2zero{$client[State][muteLine]};
	_Class purge tmp/core/prompt/lines;
}
/* }}} */

/* Hook to Tick Event to Update Prompt Clock {{{ */
_Event_register {tick} {promptTick} {_PromptTick};

/* To be called whenever the time is updated */
#alias {_PromptTick} {
	#nop %1;
	#format prompt[$prefs[split][bot]] {%s%s} {$prompt[b]} {$gtime[print]};
	#echo {{%.80s}{4}} {$prompt[$prefs[split][bot]]};
}
/* }}} */

/* Handle Special "Continue" Prompt {{{ */
#alias {_promptContinue} {
	#nop %1;
	#if {$client[State][autoContinue]} {
		#line gag;
		#send {continue}
	};
}
/* }}} */

/* Handle Special "Write" Prompt {{{ */
#alias {_promptWrite} {
	#nop %1;
}
/* }}} */

/* Dummy to do nothing on a unknown prompt type {{{ */
#alias {_promptUnknown} {
	#nop %1;
}
/* }}} */

/* Verify loading */
#var loadState {1};
/* EOF */
