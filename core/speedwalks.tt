#nop core/speedwalks.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: speedwalks;
#nop PROVIDES: core/speed;
/* 
	Sets up Speedwalk Sub-System
*/

/* Player Commands {{{ */
#alias {^run$} {
	_Class load tmp/core/sw/run core/speedwalks/rtlib/run.tt;
	_RunSpeedWalk;
	_Class purge tmp/core/sw/run;
}

#alias {^run!$} {
	_Class load tmp/core/sw/run core/speedwalks/rtlib/run.tt;
	_RunSpeedWalkNoCheck;
	_Class purge tmp/core/sw/run;
}

#alias {^stop$} {
	#if {$stateChar[is_onSpeedWalk]} {
		#nop TODO Save coordinates here;
		#echo {<110>Stop: Aborting Speedwalk.\n<099>Resume with 'run'};
		_SpeedWalkCleanup;
		_Event_raise {speedwalk_aborted};
	} {
		#echo {<110>Stop: You're not on a speedwalk.};
	};
}

#alias {^goto$} {
	_Class load tmp/core/sw/goto core/speedwalks/rtlib/goto.tt;
	_gotoHelp;
	_Class purge tmp/core/sw/goto;
}

#alias {^goto %*} {
	#if {"%1" != ""} {
		#regex {%1} {%d %d} {
			_Class load tmp/core/sw/goto core/speedwalks/rtlib/coord.tt;
			_goto_coord {&1} {&2};
		} {
			_Class load tmp/core/sw/goto core/speedwalks/rtlib/goto.tt;
			_realgoto {%0};
		};
	} {
		_gotoHelp;
	};
	_Class purge tmp/core/sw/goto;
}

#alias {v{|e|en|end|endo|endor} go %*} {
	_Class load tmp/core/sw/vendor core/speedwalks/rtlib/vendorwalk.tt;
	_vendorwalk %2;
}
/* }}} */

/* Internal Subroutines {{{ */
/* Chain paths on the speedwalk queue or cleanup if queue is empty */
#event {END OF PATH}
{
	#if {@std_queue_depth{_pathQueue}} {
		#echo {<159>Loading next state of speedwalk...<099>};
		_Event_raise {speedwalk_loadnext};
		_loadPathQueue;
	} {
		_SpeedWalkCleanup;
		_Event_raise {speedwalk_finished};
	};
}

/* Cleanup if aborting or finished with speedwalking */
#alias {_SpeedWalkCleanup}
{
    #var stateChar[is_onSpeedWalk] {0};
    #send {set superbrief off};  
}

/* Setup called before starting speedwalking */
#alias {_SpeedWalkRun}
{
	_Event_raise {speedwalk_start};
	#var stateChar[is_onSpeedWalk] {1};
    #send {set superbrief on};
    #path walk;
    #unvar __ready;
} 

/* Handles Confirmation of 'run' command */
#alias {_SpeedWalkRunConfirm} {
	#nop %1;
	#switch {"%2"} {
		#case {"y"} {
			#echo {<110>Running speedwalk...<099>};
			_SpeedWalkRun;
		};
		#default {
			#echo {<119>Not running speedwalk.<099>};	
		};
	};
};

/* Check to see if you should take the next step
   if you've been attacked, or fell abort
*/
_Event_register {speedStep} {sw_walker} {_SpeedWalkStep};

#alias {_SpeedWalkStep}
{
	#nop %1;
	#if {$stateChar[Combat]}
	{
	 #echo {<110>You have been attacked! Aborting Speedwalk!\n<099>Resume with 'run'};
		_SpeedWalkCleanup;
		_Event_raise {speedwalk_aborted};
	} {
		#path walk;
	};
}


#alias {_loadPathQueue} {
	#if {@std_queue_depth{_pathQueue}} {
		_Class load tmp/core/sw/paths data/paths.tt;
		#path load @std_queue_pop{_pathQueue};
		_Class purge tmp/core/sw/paths;
	};
}
/* }}} */

/* Verify loading */
#var loadState {1};
/* EOF */
