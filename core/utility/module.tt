#showme {<aef>Loading: <faa>utility/modules.tt}
/*
	insmod <module name>
	lsmod loaded|all
	rmmod <module name>
*/

/* Wrapper Aliases to be called to use the module system {{{ */
#alias {insmod} {
	#class tmp_module read core/utility/lib/module.tt;
	module_insert %1;
	#class tmp_module kill;
}

#alias {rmmod} {
	#class tmp_module read core/utility/lib/module.tt;
	#if {"%1" == "all" } {
		module_remove_all;
	} {
		module_remove %1;
	};
	#class tmp_module kill;
}

#alias {lsmod} {
	#class tmp_module read core/utility/lib/module.tt;
	module_list %1;
	#class tmp_module kill;
}

#alias {helpmod} {
	#class tmp_module read core/utility/lib/module.tt;
	module_help %1;
	#class tmp_module kill;	
}
/* }}} */

#function {std_module_loaded} {
	#math result @std_list_isMember{ {%1} {_modules[loaded]} };
}

#function {std_module_desc} {
	#script result {cat modules/%1/desc 2>/dev/null};
	#return $result[1];
}

/* All persistent values should be saved in $mod[mod_name][%*] {{{ */
_event_register {session_connect}    {moduleRecallLoad} {_std_modules_load};
_event_register {session_connect}    {zmoduleAutoLoad}  {_std_modules_auto};
_event_register {session_disconnect} {moduleRecallSave} {_std_modules_save};

#alias {_std_modules_auto} {
	#nop %1;
	#echo {<139>Autoloading %m modules.} {&{_modules[AutoLoad][]}};
	#foreach {${_modules[AutoLoad][]}} {__mod} {
		insmod ${_modules[AutoLoad][${__mod}]};
	};
}

#alias {_std_modules_save} {
	#nop %1;
	#echo {<139>Saving persistent module data.};
	std_value_save {mod};
}

#alias {_std_modules_load} {
	#nop %1;
	#echo {<139>Loading persistent module data.};
	std_value_load {mod};
}
/* }}} */
