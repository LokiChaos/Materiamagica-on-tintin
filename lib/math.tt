#nop lib/math.tt
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: math;
#nop PROVIDES: lib/math;

/*
	Usage: @std_math_eval{ <math expression> }
	Returns: the value of the expression after evaluation
*/
#function {std_math_eval} {
	#math result {%0};
}

/*
	Usage: @std_math_max{ {<value>} {<value>} }
	Returns: the value of the greater input value
*/
#function {std_math_max} {
	#if {%1 > %2} {
		#return %1;
	} {
		#return %2;
	};
}

/*
	Usage: @std_math_min{ {<value>} {<value>} }
	Returns: the value of the lesser input value
*/
#function {std_math_min} {
	#if {%1 > %2} {
		#return %2;
	} {
		#return %1;
	};
}

/*
	Usage: @std_math_abs{ <value> }
	Returns: the absolute value of the argument
*/
#function {std_math_abs} {
	#math result { @std_math_sign{%0} * %0};
}

/*
	Usage: @std_math_sign{ <value> }
	Returns: 1 if value is positive, -1 if negative, and 0 if zero
*/
#function {std_math_sign} {
	#if { %0 > 0} {
		#var result 1;
	};
	#elseif { %0 < 0 } {
		#var result {-1};
	};
	#else {
		#var result 0;
	};
}

/*
	Usage: @std_decrement{ <value> }
	Returns: the value--
*/
#function {std_decrement} {
	#math result {%1 - 1};
}

/*
	Usage: @std_dec2zero{ <value> }
	Returns: the value-- or 0, whichever is greater
*/
#function {std_dec2zero} {
	#var result @std_math_max{ {0} {@std_decrement{%1}} };
}

/* Verify loading */
#var loadState {1};
/* EOF */
