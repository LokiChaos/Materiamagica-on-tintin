#nop init/login/login.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: init/login;

/* Auto-Send Character Name, if it fails twice abort the auto-login & clean up */
#act {^By what name shall we know thee?} {
	_Class purge init/login/telopt;
	#send {$charName};
	#math __nameTry {${__nameTry} + 1 };
	#if {${__nameTry} > 2} {
		_Class purge tmp/init/login;
	};
}

/* tt++ never knows your password, it only reads it in
   from a file and kills the action for safety */
#act {^Password:} {
	#unvar __nameTry;
	#textin char/$sessionName/pass;
	#unact {^Password:};
}

/* Only auto-respond to the login menu if menu option is >0 */
#act {^Your choice?} {
	#delay {login_auto_choice} {
		#if {${_login_menu_choice} > 0} {
			#send {${_login_menu_choice}};
		} {
			#if {$client[Config][loginChoice] > 0} {
				#send {$client[Config][loginChoice]};
			};
		};
	} {5};
	#unact {^Your choice?};
}

/* Enter the world and Cleanup this class to negate any chance of abuse */
#act {^I-MOTION Engine v%d.%d (build %d)$} {
	#undelay login_auto_choice;
	_Class purge tmp/init/login;
	#unvar _login_menu_choice;
	_Event_raise {session_login} {login};
}

/* Cleanup this class to negate any chance of abuse */
#act {^Welcome back to Materia Magica.} {
	_Class purge tmp/init/login;
	#unvar _login_menu_choice;
	_Event_raise {session_login} {reconnect};
}
