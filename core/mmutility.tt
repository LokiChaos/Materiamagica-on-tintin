#nop core/mmutlity.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: mmutility;
#nop PROVIDES: core/mmutil;
/*
	Matria magica specific helpers.
*/

#function {mmcolor2tt} {
	#var __colorConvertTbl {
	    {|B}{<149>}{|R}{<119>}{|C}{<169>}{|M}{<159>}{|Y}{<139>}{|W}{<179>}{|G}{<129>}
		{|b}{<049>}{|r}{<019>}{|c}{<069>}{|m}{<059>}{|y}{<039>}{|w}{<079>}{|g}{<029>}
		{|D}{<109>}
	    {|I|B}{<149><788>}{|I|R}{<119><788>}{|I|C}{<169><788>}{|I|M}{<159><788>}
		{|I|Y}{<139><788>}{|I|W}{<179><788>}{|I|G}{<129><788>}
		{|I|b}{<049><788>}{|I|r}{<019><788>}{|I|c}{<069><788>}{|I|m}{<059><788>}
		{|I|y}{<039><788>}{|I|w}{<079><788>}{|I|g}{<029><788>}
		{|I|D}{<109><788>}

		};
	#var result ${__colorConvertTbl[%1]};
	#unvar __colorConvertTbl;
}

/* Colorizes Exit String */
/* TODO: unhardcode colors and move to settings */
#function {exits_colorize} {
	#var result[exit] {%0};
	#var result[eCol] {
		{Dk}{<039>}
		{CPK}{<111>}
		{NPK}{<119>}
		{LPK}{<119>}
		{DkCPK}{<001>}
		{DkNPK}{<119>}
		{DkLPK}{<019>}
	};

	#replace result[exit] {({[NSEWUDew]+})} {<159>&1<139>};
	#foreach {${result[eCol]}} {result[idx]} {
		#replace result[exit] {{[NSEWUDew]+}[${result[idx]}]} {${result[eCol][${result[idx]}]}&1<139>};
	};
	#replace result[exit] {?} {<128>?};
	#replace result[exit] {None.} {<128>None};

	#if {&{navigation[lastDir]}} {
		#format result[ld] {%n} {$navigation[lastDir]};
		#replace result[exit] {>${result[ld]}<} {><488>${result[ld]}<088><}
	};

	#format result[exit] {%.22s} {$result[exit]};
	#format result {<139>%-22s<099>} {$result[exit]};
}

/* Scroll <-> Spell Lookups from the data table {{{ */
#function {spell2scroll} {
	#script result {awk -F : 'tolower($1) == "%0" {print $4}' data/spells.txt};
	#return $result[1];
}

#function {scroll2spell} {
	#script result {awk -F : 'tolower($4) == "%0" {print $1}' data/spells.txt};
	#return $result[1];
}
/* }}} */

/* Zone/Location Lookups from data table {{{ */
#function {repopTime} {
	#script result {awk -F: '$1 == "@string_escape_shell{%0}" {print $12}' data/locations.txt};
	#if {&{result[]} && "${result[1]}" != "NA"} {
		#var result ${result[1]};
	}{
		#var result 0;
	};
}

#function {zoneType} {
	#script result {awk -F: '$1 == "@string_escape_shell{%0}" {print $5}' data/locations.txt};
	#if {&{result[]}} {
		#var result ${result[1]};
	}{
		#var result {};
	};
}
/* }}} */

/* Verify loading */
#var loadState {1};
/* EOF */
