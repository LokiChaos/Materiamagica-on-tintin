#nop core/activies/questing.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: activities;

/* Quest Status (With Multiple Quests) {{{ */
#act {^Quest #%d: %* (%d/%d goal{|s} complete).$} {
	#math __qIdx {${__qIdx} + 1};
	#if {&{_quests[%1]} == 0 } {
		#var _quests[%1][name]   {%2};
		#var _quests[%1][phCom]  {%3};
		#var _quests[%1][phTot]  {%4};
	};
	#var _quests[%1][index] {${__qIdx}};
	#var __questNum {%1};
	#line gag;
	#echo {%+2s<149>) <099>%2 <139>(<099>%1<139>) [<099>%3<139>/<099>%4<139>]<099> phases complete.} {${__qIdx}};
}

#act {^  This quest has no time limit.$} {
	#var _quests[${__questNum}][timeRem]  {-1};
	#line gag;
	#echo {    This quest has no time limit.};
};

#act {^  Time Remaining: %d Alyrian minutes.$} {
	#var _quests[${__questNum}][timeRem]  {%1};
	#line gag;
	#echo {    Time Remaining: <139>%s<099> Alyrian Minutes.} {%1};
};

#act {^A total of [%d] quests.$} {
	#unvar __qIdx;
	#unvar __questNum;
}
/* }}} */

/* Quest Status (No Quests) {{{ */
#act {^You aren't currently on any quests.$} {
	#var _quests[list] {};
	#var _questsLast {-1};
}
/* }}} */

/* Either quest status #, quest status when on only one quest, or just given a quest {{{ */
#act {^This quest [%d] is called '%*',$} {
	#if {&{_quests[%1][]}} {
		#nop;
	} {
		#var _quests[%1] {};
	};
	#var _quests[%1][name] {%2};
	#var _quests[%1][phCom] {0};
	#var _questInfo {%1};

	_Class load tmp/core/activites/questInfo core/activities/rtlib/questinfo.tt;
	_event_oneshot {prompt_post} {zquestInfoClean} {_questInfoFin};

	#if {"${_quest_status}" != "1"} {
		_Class load tmp/core/activities/questRequest core/activities/rtlib/questreq.tt;
		_event_oneshot {prompt_post} {questReqClean} {_questRequestFin};
	};
};
/* }}} */

/* Running out of time and Losing a Quest {{{ */
#act {^You have %d Alyrian minute{|s} remaining to finish your quest, '%*'.$} {
	_event_raise {activity_quest} {time} {%2} {%1};
}

#act {^You have run out of time for your quest, '%*'.} {
	#format {__questEcho} {<049>You have run out of time for the quest '%s'.} {%1};
	_CaptureLog {${__questEcho}} {$capPath[act]};
	#unvar __questEcho;
	#unvar _quests[@quest_name_to_num[%1]];
	_event_raise {activity_quest} {fail} {time} {%1};
}

#act {^You are no longer on the quest, '%*'.$} {
	#format {__questEcho} {<049>You are no longer on the quest '%s'.} {%1};
	_CaptureLog {${__questEcho}} {$capPath[act]};
	#unvar __questEcho;
	#unvar _quests[@quest_name_to_num[%1]];
	_event_raise {activity_quest} {fail} {other} {%1};
}
/* }}} */

/* Phase Completions {{{ */
#act {^All parts of your quest are complete. Return to your quest master in time to receive your reward!$} {
	_CaptureLog {<049>All Quest Phases Completed!} {$capPath[act]};
	_event_raise {activity_quest} {phase} {complete} {unknown};
}

#act {^All parts of your quest '%*' are complete.$} {
	_CaptureLog {<049>All Quest Phases Completed! (%1)} {$capPath[act]};
	_event_raise {activity_quest} {phase} {complete} {%1};
}

#act {^You have completed a part of your quest!$} {
	_event_raise {activity_quest} {phase} {done} {unknown};
}

#act {^You have completed a part of your quest, '%*'!$} {
	_event_raise {activity_quest} {phase} {done} {%1};
}

#act {^Quest Goal: %* [%d/%d Complete].$} {
	_event_raise {activity_quest} {phase} {partial};
}
/* }}} */

/* Requesting a Quest {{{ */
#act {^You ask %* for a quest.$} {
	_Class load tmp/core/activites/questRequest core/activities/rtlib/questreq.tt;
	_event_raise {activity_quest} {requesting} {%1};
	_event_oneshot {prompt_post} {questReqClean} {_questRequestFin};
}
/* }}} */

/* Turning in a Quest {{{ */
#act {^You inform %* you have completed your quest '%*'.$} {
	_Class load tmp/core/activities/questComplete core/activities/rtlib/questcom.tt;
	#var _questComplete[master] {%1};
	#var _questComplete[name] {%2};
	#unvar _quests[@quest_name_to_num{%2}];
	_event_oneshot {prompt_post} {questComClean} {_questCompleteFin};
}
/* }}} */

/* Get quest ID# from name or index {{{ */
#function {quest_name_to_num} {
	#var result -1;
	#foreach {${_quests[]}} {__qNum} {
		#if { "${_quests[${__qNum}][name]}" == "%1" } {
			#var result {${__qNum}};
		};
	};
	#unvar __qNum;
}

#function {quest_index_to_num} {
	#var result -1;
	#foreach {${_quests[]}} {__qNum} {
		#if { "${_quests[${__qNum}][index]}" == "%1" } {
			#var result {${__qNum}};
		};
	};
	#unvar __qNum;
}
/* }}} */

/* Wrapper for the quest command {{{ */
#alias {quest} {
	_Class load tmp/core/activities/questCmd core/activities/rtlib/questing.tt;
	_quest %0;
	_Class purge tmp/core/activities/questCmd;
}
/* }}} */
