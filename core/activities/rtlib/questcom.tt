#nop core/activities/lib/questcom.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: activities/questcom;

#act {^%* tells you, 'You didn't complete any parts of your quest, I'm sorry.'$} {
	_Event_raise {activity_quest} {complete} {none} {${_questComplete[name]}};
	#format {__questEcho} {<049>Rejected Quest '%s' from %s} {${_questComplete[name]}} {${_questComplete[master]}};
	_CaptureLog {${__questEcho}} {$capPath[act]};
	#unvar __questEcho;
	#unvar _questComplete;
}

#act {^%* tells you, 'As a reward for thy valor, I am awarding you %d quest points.'} {
	#format {__questEcho} {<049>Partially Completed %s's Quest '%s,' rewards: %d quest points.}
	        {${_questComplete[master]}} {${_questComplete[name]}} {%2};
	_CaptureLog {${__questEcho}} {$capPath[act]};
	#unvar __questEcho;
	_Event_raise {activity_quest} {complete} {partial} {${_questComplete[name]}};
	#unvar _questComplete;

}

#act {^%* tells you, 'As a reward for thy valor, I am awarding you %d quest points, %*.'} {
	#format {__questEcho} {<049>%s's Quest '%s' Completed, rewards: %d quest points, %s.}
	        {${_questComplete[master]}} {${_questComplete[name]}} {%2} {%3};
	_CaptureLog {${__questEcho}} {$capPath[act]};
	#unvar __questEcho;
	_Event_raise {activity_quest} {complete} {full} {${_questComplete[name]}};
	#unvar _questComplete;
}

#alias {_questRecord}
{
	@std_value_restore{lastQuest};
	#format timeNow {%T};
	#var lastQuest[%1][2] {$lastQuest[%1][1]};
	#var lastQuest[%1][1] {$timeNow};
	#list lastQuest[Main]  {insert} {1} {$timeNow};
	#if {&lastQuest[Main] > 7} {
		#list lastQuest[Main]  {delete} {8};
	}
	@std_value_save{lastQuest};
	#unvar timeNow;
};

#alias {_questCompleteFin} {
	#nop %1;
	_Class purge tmp/core/activites/questComplete;
}

/* Verify loading */
#var loadState {1};
/* EOF */
