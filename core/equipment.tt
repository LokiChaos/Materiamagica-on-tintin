#nop core/equipment.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: equip;
#nop PROVIDES: core/equip;

/* Player Commands {{{ */
/* Displays just AR output from eq command */
#alias {ar}
{
	#class tmp_silentEq read core/equipment/armor.tt;
	equipment;
	#var stateClient[muteLine] 1;
}

#alias {weapon}
{
	#class tmp_silentEq read core/equipment/weapon.tt;
	equipment;
	#var stateClient[muteLine] 1;
}
/* }}} */

/* Collect info from eq[ipment] command {{{ */
#act {^You are using:}
{
	#class tmp_info_eq read core/equipment/eq.tt;
	_event_oneshot {prompt_post} {eqClean} {#class tmp_info_eq kill; #nop
	};
}
/* }}} */

/* Internal Helper - executes equipment command & gags output {{{ */
#alias {_equipSilent}
{
	_event_oneshot {prompt_post} {clearAutoCont} {#var stateClient[autoContinue] 0; #nop
	};
	#class tmp_silentEq read core/equipment/silentEquip.tt;
	equipment;
	#var stateClient[muteLine] 1;
	#var stateClient[autoContinue] 1;
}
/* }}} */

/* Equipping and Removing items {{{ */
/* When you hold a weapon it updates info with a silent equipment command */
#act {^You wield %* in your %w hand.$} {
	_equipSilent;
	#var stateClient[muteLine] 2;
	#echo {<149>Updating weapon info...};
	_event_raise {equip} {weild} {%1} {%2 hand};
}

#act {^You hold %* in your %w hand.$} {
	_event_raise {equip} {equip} {%1} {%2 hand};
} {4.9}

#act {^You hold %*.$} {
	_event_raise {equip} {equip} {%1} {used as light};
}

#act {^You stop using %*.} {
	_event_raise {equip} {remove} {%1};
}

#act {%* sparks and sputters, and a thin trail of smoke dissipates into the air.$} {
	_event_raise {equip} {spark} {%1};
}

#act {^You wear %*.$} {
	#regex {%1} {%* on your %*} {
		_event_raise {equip} {equip} {&1} {worn on &2};
	};
	#regex {%1} {%* about your %*} {
		_event_raise {equip} {equip} {&1} {worn about &2};
	};
	#regex {%1} {%* around your %*} {
		_event_raise {equip} {equip} {&1} {worn around &2};
	};
	#regex {%1} {%* as a shield.} {
		_event_raise {equip} {equip} {&1} {worn as shield};
	};
}
/* }}} */

/* Hide/Find Bury/Unearth Items {{{ */
#act {^You found a hidden item, %*!$} {
	_event_raise {item} {found} {%1};
}

#act {^You have unearthed %*!$} {
	_event_raise {item} {dugup} {%1};
}

#act {^You successfully hide %*.$} {
	_event_raise {item} {hide} {%1};
}

#act {^You have successfully buried %*.$} {
	_event_raise {item} {bury} {%1};
}
/* }}} */

#act {^The Powers give you %S gold coins for your sacrifice of %*.$}{
	_event_raise {item} {sacrifice} {%2} {%1};
}

/* Get/Put/Drop/Locker items {{{ */
#act {^You get %* from your locker.$} {
	_event_raise {item} {get} {locker} {%1} {1};
} {4.5}

#act {^You take %d of %* from %*.$} {
	_event_raise {item} {get} {container} {%2} {%1} {%3};
} {4.6}

#act {^You take %d of %*.$} {
	_event_raise {item} {get} {ground} {%2} {%1};
} {4.8}

#act {^You take %* from %*.$} {
	_event_raise {item} {get} {container} {%1} {1} {%2};
} {4.7}

#act {^You take %*.$} {
	_event_raise {item} {get} {ground} {%1} {1};
} {4.9}

#act {^You put %* in your locker$}{
	_event_raise {item} {put} {locker} {%1} {1};
} {4.5}

#act {^You put %d of %* in %*.$} {
	_event_raise {item} {put} {container} {%2} {%1} {%3};
} {4.6}

#act {^You put %* in %*.$} {
	_event_raise {item} {put} {container} {%1} {1} {%2};
} {4.9}

#act {^You drop %d of %*.$} {
	_event_raise {item} {drop} {%2} {%1};
} {4.8}

#act {^You drop %*.$} {
	_event_raise {item} {drop} {%1} {1};
} {4.9}
/* }}} */

/* Out of weight or count carry capacity {{{ */
#act {^%*: there's not enough room in your inventory.$} {
	_event_raise {item} {inv_noroom};
}

#act {^%*: you cannot carry that much weight.$} {
	_event_raise {item} {inv_noweight};
}

#act {^You can't store a container in a container.$} {
	_event_raise {item} {container_invalid};
}

#act {^%* can hold a maximum of %d items, and is already full.$} {
	_event_raise {item} {container_noroom};
}

/* }}} */

/* Item Binding (both on take and on wear) {{{ */
#act {%* emits a bright flare of white light as it binds with you.$} {
	_event_raise {item} {bound} {%1};
}
/* }}} */

/* Collect info from inventory command {{{ */
#act {^You are carrying:$} {
	#class tmp_info_inv read core/equipment/inventory.tt;
	#var stateChar[Encumbered] {0};
	_event_oneshot {prompt_post} {invClean} {#class tmp_info_inv kill; #nop
	};
}

#act {^{\* |}You are currently encumbered. You may move slower than normal.} {
	#var stateChar[Encumbered] {1};
}
/* }}} */

/* Disarmed */
#act {^%* knocks %* from your grip.$} {
	_event_raise {equip} {disarmed} {%2} {%1};
	_Notice {<119>DISARMED<099> - %3};
};
