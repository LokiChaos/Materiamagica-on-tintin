#nop core/speedwalks/vendorwalk.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: speedwalks/vendor;
/*
	Loads a path to the named vendor into the speedwalk queue
*/

#alias {_vendorwalk %*}
{
	#unvar _pathQueue;
	#act {^You sense that '%*' (Managed by %*) may be located %*.$} {
		#line gag;
		#var __vendorpath {%%3};
		#replace {__vendorpath} {, } {;};
		#format {__vendorpath} {%sFIN} {${__vendorpath}};
		#replace {__vendorpath} {;%wFIN} {;open &1; &1};
		#path load {${__vendorpath}};
		#unvar __vendorpath;
		#echo {<149>Path to shop '%s' owned by %s has been loaded. Loaded path... } {%%1} {%%2};
		_Class purge tmp/core/sw/vendor;
	};
	#act {^There is no vendor leased by a character matching that name.} {
		_Class purge tmp/core/sw/vendor;
	};
	#act {^There is no vendor leased by a clan of that number.} {
		_Class purge tmp/core/sw/vendor;
	};
	#act {^You are not able to sense the direction of that vendor from your current location.$} {
		_Class purge tmp/core/sw/vendor;
	};
	#send {vendor where %1};
}

/* Verify loading */
#var loadState {1};
/* EOF */
