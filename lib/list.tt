#nop lib/list.tt
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: list;
#nop PROVIDES: lib/list;
#nop REQUIRES: lib/print;

/*
	Note about arguments:
		{<list>} means pass-by-ref and the argument should be a variable name of the table
		{<$list>} means pass-by-value and the argument should be a table
*/


/*
	Usage: @list_length{ {<list>} }
	<list> = name of list
	Returns: length of list
*/
#function {list_length} {
	#if {"%1" == ""} {
		Print {ERROR} {list_findKey: argument error};
		#var result {error};
	};
	#else {
		#var result {&{%1}};
	}
}

/*
	Usage: @list_getNthKey{ {<index>} {<list>} }
	<index> = integer value (positive of negative)
	<list> = name of list
	Returns: key at position <index> negative values will return relative to end of list
	         Or {} if there is no <index>th key in <list>
*/
#function {list_getNthKey} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_getNthKey: argument error};
		#var result {error};
	};
	#else {
		#list _:ListKeys create ${%2[]};
		#var result {${_:ListKeys[%1]}};
		#unvar _:ListKeys;
	};
}

/*
	Usage: @list_getNthKeypair{ {<index>} {<list>} }
	<index> = integer value (positive of negative)
	<list> = name of list
	Returns: key pair at position <index> negative values will return relative to end of list
	         Or {} if there is no <index>th keypair in <list>
*/
#function {list_getNthKeypair} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_getNthKey: argument error};
		#var result {error};
	};
	#else {
		#list _:ListKeys create ${%2[]};
		#var result {${_:ListKeys[%1]}};
		#unvar _:ListKeys;
		#var result {{key}{$result}{value}{${%2[$result]}}}
	};
}

/*
	Usage: @list_findKey{ {<key>} {<list>} }
	Returns: 0 if <key> isn't in list, else it returns the positional index
*/
#function {list_findKey} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findKey: argument error};
		#var result {error};
	};
	#else {
		#list _:ListKeys create ${%2[]};
		#list _:ListKeys find {%1} {result};
		#unvar _:ListKeys;
	};
}

/*
	Usage: @list_isKey{ {<key>} {<list>} }
	Returns: 0 if <key> isn't in list, of 1 if it is.
*/
#function {list_isKey} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_isKey: argument error};
		#var result {error};
	};
	#else {
		#list _:ListKeys create ${%2[]};
		#math result { 0 != @list_findValue{{%1}{_:ListKeys}} };
		#unvar _:ListKeys;
	};
}

/*
	Usage: @list_findValue{ {<value>} {<list>} }
	Returns: 0 if value isn't in list, else it returns the position
	Note: it returns the FIRST match to <value>
*/
#function {list_findValue} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findValue: argument error};
		#var result {error};
	};
	#else {
		#list {%2} {find} {%1} {result};
	};
}

/*
	Usage: @list_findValueKey{ {<value>} {<list>} }
	Returns: {} if value isn't in list, else it returns the key
	Note: it returns the FIRST match to <value>
*/
#function {list_findValueKey} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findValueKey: argument error};
		#var result {error};
	};
	#else {
		#list {%2} {find} {%1} {_:ListKey};
		#list _:ListKeys create ${%2[]};
		#var result {${_:ListKeys[${_:ListKey}]}};
		#unvar _:ListKeys;
		#unvar _:ListKey;
	};
}

/*
	Usage: @list_isValue{ {<value>} {<list>} }
	Returns: 0 if <value> is not is <list> or 1 if it is.
*/
#function {list_isValue} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_isValue: argument error};
		#var result {error};
	};
	#else {
		#math result { 0 != @list_findValue{{%1}{%2}} || "${%2[0]}" == "%1" };
	};
}

/*
	Usage: @list_findAllValue{ {<value>} {<list>} }
	Returns: {} if value isn't in list, else it returns a list of all indexes with matching value
*/
#function {list_findAllValue} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findAllValue: argument error};
		#var result {error};
	};
	#else {
		#var result {};
		#loop {1} {&{%2[]}} {_:ListPos} {
			#if {"${%2[+${_:ListPos}]}" == "%1"} {
				#list result add {${_:ListPos}};
			};
		};
		#unvar _:ListPos;
	};
}

/*
	Usage: @list_findAllValueKey{ {<value>} {<list>} }
	Returns: {} if value isn't in list, else it returns a list of all keys with matching value
*/
#function {list_findAllValueKey} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findAllValueKey: argument error};
		#var result {error};
	};
	#else {
		#var result {};
		#forall {${%2[]}} {
			#if {"${%2[&0]}" == "%1"} {
				#list result add {&0};
			};
		};
	};
}

/*
	Usage: @list_findAllValueCount{ {<value>} {<list>} }
	Returns: 0 if value isn't in list, else it returns a count of how many times it appears
*/
#function {list_findAllValueCount} {
	#if {"%1" == "" || &{%2} == 0} {
		Print {ERROR} {list_findAllValueKey: argument error};
		#var result {error};
	};
	#else {
		#var result {};
		#forall {${%2[]}} {
			#if {"${%2[&0]}" == "%1"} {
				#list result add {&0};
			};
		};
	#var result {&{result[]}};
	};
}

/*
	Usage: @list_swapPair{ {<list>} }
	Flips key <=> values
	Returns: a list based on <list> with the keys and values swapped
	Note:
		* Does not preserve ordering
		* Will omit any pairs with a table value
		* Will behave oddly for duplicate values
*/
#function {list_swapPair} {
	#if {&{%1} == 0} {
		Print {ERROR} {list_swapPair: argument error};
		#var result {error};
	};
	#else {
		#var result {};
		#forall {${%1[]}} {
			#if {${%1[&0][]} == 0} {
				#var result[${%1[&0]}] {&0};
			} {
				Print {DEBUG} {list_swapPair: omitting table value @ key '&0'};
			};
		};
	};
}

/* Verify loading */
#var loadState {1};
/* EOF */
