#nop core/captures/channels.tt;
/*
	Author: LokiChaos Â©2009-2016 Released under the ISC License (See doc/license.txt)
*/
#nop CLASS: captures;
#nop PROVIDES: core/captures/channels;
#nop REQUIRES: core/captures;

/* Clan Talk {{{ */
#act {@sgrInit{}^@sgrp{!Y}[CLAN] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[clanTalk]};
	#regex {%0} {[CLAN] %w has entered Materia Magica.} {
		_Event_raise {clan} {login} {&1};
	};
	#regex {%0} {[CLAN] %w has recalled to the clan hall!} {
		_Event_raise {clan} {gohall} {&1};
	};
	#regex {%0} {[CLAN] %w has left Materia Magica} {
		_Event_raise {clan} {logout} {&1};
	};
	#regex {%0} {[CLAN] %w is now away.%*} {
		_Event_raise {clan} {away} {&1} {<139>&2};
	};
	#regex {%0} {[CLAN] %w is no longer away.} {
		_Event_raise {clan} {return} {&1};
	};

} {3}

#act {@sgrInit{}^@sgrp{!Y}[CLAN NOTICE] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[clanTalk]};
	_Event_raise {clan} {notice} {%0};
} {3}

#act {@sgrInit{}^@sgrp{!Y}[%d] clan member%? heard you say, %*@sgrFin{}} {
	#switch {$prefs[firstP]} {
		#case {0} {
			#format {__clancap} {<139>[%d] clan member%s heard you say, %s <099>} {%2} {%3} {%4};
		};
		#case {1} {
			#format {__clancap} {<139>[CLAN] $charName: %s <099>} {%4};
		};
		#default  {
			#format {__clancap} {<139>[CLAN] Me: %s <099>} {%4};
		};
	};
	_ChannelLog {${__clancap}} {$capPath[clanTalk]};
	#unvar __clancap;
} {3}
/* }}} */

/* Alliance Talk {{{ */
#act {@sgrInit{}^@sgrp{!Y}[ALLIED %d] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[allianceTalk]};
} {3}

#act {@sgrInit{}^@sgrp{!Y}[%d] alliance member%? heard you say, %*@sgrFin{}} {
	#switch {$prefs[firstP]} {
		#case {0} {
			#format {__allycap} {<139>[%d] alliance member%s heard you say, %s} {%2} {%3} {%4};
		};
		#case {1} {
			#format {__allycap} {<139>[ALLIED] $charName: %s} {%4};
		};
		#case {2} {
			#format {__allycap} {<139>[ALLIED] Me: %s} {%4};
		};
	};
	_ChannelLog {${__allycap}} {$capPath[allianceTalk]};
	#unvar __allycap;
} {3}

#act {@sgrInit{}^@sgrp{!Y}[ALLIED NOTICE] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[allianceTalk]};
	_ChannelLog {%0} {$capPath[allianceEvents]};
	_Event_raise {alliance} {notice} {%0};
} {3}
/* }}} */

/* PK Talk {{{ */ 
#act {@sgrInit{}^@sgrp{!D}[TALK %w] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[pkTalk]};
} {3}

#act {@sgrInit{}^@sgrp{!D}[TALK@sgrp{X}@sgrp{!D}] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[pkTalk]};
} {3}

/* }}} */

/* Tells + Pages + Prays {{{ */ 
#act {@sgrInit{}^@sgrp{!R}%* tells you %*@sgrFin{}} {
	#nop Filters out shopkeepers, far from perfect;
	#regex {%3} {'I can {sell|trade} you this item for %W {gold|reward units}.'} {
		#nop Stupid Shopkeeps;
	}{
		_ChannelLog {%0} {$capPath[tells]};
		_Event_raise {tell_recieved} {%2} {<119>%3};
	};
} {3}

#act {@sgrInit{}^@sgrp{!R}You tell %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
} {3}

#act {@sgrInit{}^@sgrp{!R}%w is {away|AWAY} and may not see your tell.@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
} {3}

#act {@sgrInit{}^@sgrp{!R}Message: %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
} {3}

#act {@sgrInit{}^\a@sgrp{!R}%* is paging you...@sgrp{X}@sgrFin{}$} {
	_ChannelLog {<119>%2 is paging you...} {$capPath[tells]};
	#var _chanPager {%2};
} {3}

#act {@sgrInit{}^@sgrp{!R}Reason: %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
	_Event_raise {page_recieved} {${_chanPager}} {<119>%2};
	#unvar _chanPager;
} {3}

#act {^You have paged %*.} {
	_ChannelLog {<099>%0} {$capPath[tells]};
} {3}

#act {@sgrInit{}^Reason: %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
} {3}

#act {@sgrInit{}^@sgrp{!R}[%d] active Guides and [%d] visible Immortals heard you pray '%*'@sgrFin{}} {
	_ChannelLog {%0} {$capPath[tells]};
} {3}
/* }}} */

/*Form Talk {{{ */
#act {@sgrInit{}^@sgrp{!C}%* tells the formation %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[formTalk]};
} {3}

#act {@sgrInit{}^@sgrp{!C}You tell the formation %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[formTalk]};
} {3}
/* }}} */

/* Archon Talk {{{ */ 
#act {@sgrInit{}^@sgrp{X}\e[1\;34\;49m[\e[22\;37\;49m%d\e[1\;34\;49m]%*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[archonTalk]};
} {3}

#act {@sgrInit{}^@sgrp{X}\e[1\;34\;49m[\e[22\;37\;49mARCHON\e[1\;34\;49m]%*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[archonTalk]};
} {3}
/* }}} */

/* Notify {{{ */
#act {^Sign{off|on} at %* for %w detected.}
{
	#if {"%1" == "on"} {
		_Event_raise {notify} {signon} {%3};
	}{
		_Event_raise {notify} {signoff} {%3};	
	};
} {3}
/* }}} */

/* Guide Talk / Novice Channel TODO: Test, add pray {{{ */
#act {@sgrInit{}^@sgrp{!W}[GUIDE] %*@sgrFin{}} {
	_ChannelLog {%0} {$capPath[guide]};
}

#act {@sgrInit{}^[%S] guide{|s} heard you say, '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[guide]};
}

#nop {^(%*) [%*] prays '%*'$};

#act {@sgrInit{}^@sgrp{!Y}[CLAN Novice Adventurers] %*: '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[novice]};
}
#act {@sgrInit{}^@sgrp{!Y}[%d] Novice clan member%? heard you say, '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[novice]};
}
/* }}} */

/* Shouts & Yells {{{ */
#act {@sgrInit{}^@sgrp{!M}%* {shouts|SHOUTS} '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[shout]};
} {3}

#act {@sgrInit{}^@sgrp{!M}[%S] people heard you shout '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[shout]};
} {3}

#act {@sgrInit{}^@sgrp{!Y}%* yells '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[yell]};
} {3}

#act {@sgrInit{}^@sgrp{!Y}You yell '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[yell]};
} {3}
/* }}} */

/* Say/Sayto/Wisper TODO: Test & Chat > Relay {{{ */
/* Level 5.5 To let most other triggers take priority*/
#act {@sgrInit{}^@sgrp{!C}{You|.+} {ask(| .+)|asks(| you)|exclaim(| to .+)|exclaims(| to you)|(say|whisper)(| to .+)|(says|whispers)(| to you)}, '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[say]};
	#if {"$stateChar[inChatRoom]" != ""} {
		_ChannelLog {%0} {@relayPath{$stateChar[inChatRoom]}};
	};
} {5.5}

#act {@sgrInit{}^@sgrp{!C}'%*' {you|.+} {ask(| .+)|asks(| you)|exclaim(| to .+)|exclaims(| to you)|say(| to .+)|says(| to you)}. '%*'@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[say]};
	#if {"$stateChar[inChatRoom]" != ""} {
		_ChannelLog {%0} {@relayPath{$stateChar[inChatRoom]}};
	};
} {5.5}

#act {@sgrInit{}^@sgrp{!C}'%*' {you|.+} {ask(| .+)|asks(| you)|exclaim(| to .+)|exclaims(| to you)|say(| to .+)|says(| to you)}.@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[say]};
	#if {"$stateChar[inChatRoom]" != ""} {
		_ChannelLog {%0} {@relayPath{$stateChar[inChatRoom]}};
	};
} {5.5}

#act {@sgrInit{}^@sgrp{!G}[EM] %*@sgrp{X}@sgrFin{}$} {
	_ChannelLog {%0} {$capPath[emote]};
	#if {"$stateChar[inChatRoom]" != ""} {
		_ChannelLog {%0} {@relayPath{$stateChar[inChatRoom]}};
	};
}
/* }}} */

/* Dream  {{{ */
#act {@sgrInit{}^[%d] people heard you dream, '%*'@sgrFin{}$} {
	#switch {$prefs[firstP]} {
		#case {0} {
			#format {__dreamcap} {<099>[%d] people heard you dream, '%s'<099>} {%1} {%2};
		};
		#case {1} {
			#format {__dreamcap} {<099>$charName dreams '%s'<099>} {%2};
		};
		#default  {
			#format {__dreamcap} {<099>You dream '%s'<099>} {%2};
		};
	};
	_ChannelLog {${__dreamcap}} {$capPath[dream]};
	#unvar __dreamcap;
}


#act {@sgrInit{}^%S dreams '%*@sgrp{X}@sgrFin{}'$} {
	_ChannelLog {%0} {$capPath[dream]};
}
/* }}} */

/* Chat Relay {{{ */
#function {relayPath} {
	#switch {"$prefs[relayCap]"} {
		#case {"2"} {
			#var    result {relays};
		};
		#case {"1"} {
			#format result {relay/%s}  {%1};
		};
		#default {
			#format result {%s_%s} {$capPath[relayBase]} {%1};
		};
	};
}

#act {@sgrInit{}^@sgrp{!Y}[%d] people in #%* heard you relay '%*@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%3}};
} {3}

#act {@sgrInit{}^@sgrp{!Y}%*@#%S is now relaying messages from this chat room.@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%3}};
} {3}

#act {@sgrInit{}^@sgrp{!Y}%*@#%S is no longer relaying from this chat room.%*@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%3}};
} {3}

#act {@sgrInit{}^@sgrp{!Y}%w@#%S: %*@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%3}};
} {3}

#act {^You are now relaying messages from chat room #%*.} {
	_ChannelLog {%0} {@relayPath{%1}};
} {3}

#act {@sgrInit{}^You are no longer relaying from chat room #%*.%*@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%1}};
} {3}

#act {@sgrInit{}^You are banned from chat room #%*.%*@sgrFin{}} {
	_ChannelLog {%0} {@relayPath{%1}};
} {3}
/* }}} */

/* Verify loading */
#var loadState {1};
/* EOF */
